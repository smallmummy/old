---
layout:     post
title:      Students Score Statistics and Anylysis
subtitle:   Base on My Teaching Career with SAS
date:       2019-04-23
author:     Vincent C
header-img: img/post-bg-digital-native.jpg
catalog: true
tags:
    - SAS
---  

# Students Score Statistics and Anylysis Base on My Teaching Career with SAS

<br/>
## Foreword
I've been working a mandarin teacher for 5 years. During the period, lots of data of students scores which are sutiable for analysis were accumulated.   
In this article, I will perform the statistics and analysis via software SAS. In the last part, lots of diagram will be displayed and some conclusion will come out, which be valiadated with my teaching experience.  

ps:  
Besides a mandarin teaching at school, I am IT workman as well. Hence the original data for statistics and the skill of using SAS software existed, I cannot miss this chance for practice, especially which was relavent tightly with my life.

<br/>
## 1.Original Data Overview

### 1.1 Grade which I tought
 
Academic Year | Grade 1 | Grade 2 | Grade 3  
--------|--------|--------|--------  
2014-2015|Primary 5|Primary 6|Year 7
2015-2016|Primary 5|Primary 6|Year 7
2016-2017|Primary 4|Primary 5|
2017-2018|Primary 5|Primary 6|
2018-2019|Primary 5|Primary 6|

There are 4 terms in an academic year, however I dont think the data of an academic year are enough for analyzing some statistical indicators, furthermore there are some factors influences, such as the original data for the first second academic are simple and uncompleted. As a conclusion, I decide only perform the statistics on these following grade and academic years:

* 2014-2015 P5 & 2015-2016 P6  
* 2016-2017 P5 & 2017-2018 P6  
* 2016-2017 P4 & 2017-2018 P5 & 2018-2019 P6  

Note:
There are 3 rows above which has enough and completed orginal data for me to perform, and the students in the same row,during the different academic year are some student.  
For instance, the 3rd row:  
>2016-2017 P4 & 2017-2018 P5 & 2018-2019 P6  

the students who were in P4 at 2016-2017, after upgraded into the new grade, are the same student with P5 at 2017-2018. Then same for P6 at 2018-2019.  

### 1.2 Mix Student After Upgrade into a New Grade
Although the student after upgrade into a new grade are some with the previous in old grade (the transfer out and transfer in student and suspension student are the changes), but the students will mixed and assign to new classes in the new grade randomly.  

like the simple illustration in the following:

students | P5 Class 
-------|-------
A|P5-1
B|P5-1
C|P5-2
D|P5-2

after mixed and new assignment in P6 grade:

students | P5 Class 
-------|-------
A|P6-1
B|P6-2
C|P6-1
D|P6-2


to the certain extents, this is a good mechaism and good for student and school on some certain aspects, such as it will let students make more new good students and rejuvenate some classed and so on.  
howevere it also generates some disadvantages and it will be display later. Lets see it in the following chapters.


ps:  
school manegement stated the assigement after mix in the grade is base on random, as I as the one who was involved, I was clear about the fact is not like what they mention, later it will be displayed in the statistics report and diagram.  
It happened to validate some conclusion. :)  

### 1.3 Stat Items
Each term in an academic year contains:

1. Dictation Score  
Dictation in mandarin test which require student write down the mandarin characters and coresponding english meaning and some short sentence only, can indicate the level of mandarin characters writing on some extent. however high score of dictation sometimes might not express student grab how to use those mandarin words in practical sence.  
After every single lesson finished, the dictation would be executed.

2. Daily Performance Score
Subject teacher will evaluate this score base on the student daily performance, such as respone to question in class, whether chat or disturb others, whether submit homework on time, the quality of homework and etc.  
This score is subjective on different teachers, and also teacher to different students, so weight of this score cannot be too high to render the statistical indicators.  
Each term has a daily performance score.

3. Unit Test Score 
A unit is composed several lessons, generally 2 or 3 lessons. The unit test score express the extent how student grab the knowledge in a unit, and was generated from a unit test paper which consist of various kinds of question, can evaluate student level objective.  
Each term has at least one, sometimes more than one unit test.

4. Term Test Score
Every team test paper was composed by paper1 - essay, and paper2 - which consist of various kinds of questoin. As a extra, Term 2 and Term 4 test paper have oral test which could evaluate the oral ability of students.
Each team has a term test score.
 

### 1.4 Some Notes

the passing score is 70. if the student got the score lower than 70, means not passed,need a remedial. No matter how many score he or she got in the remedial, the final score recorded in the excel file is 70, however the ceil will be remarked with a distinct colour.


## 2. Data Collection
As a mandarin teacher, It's easy for me to get the original data of student score, one point to make it easy is I have the habit to record it in my computer as digital assets.  

This is a sample of one class original data:  
![sample](https://cl.ly/23fb3bb19e09/Image%2525202019-05-22%252520at%25252010.42.40%252520%2525E4%2525B8%25258B%2525E5%25258D%252588.png)  

## 3. Data Import

### 3.1 prepare library

```
libname ss_16_17 "C:\SAS_SGS\org_data\ss_p5_2016_2017";
```

### 3.2 using Import Wizard to import data

![](https://cl.ly/ae724c859638/Image%2525202019-05-23%252520at%2525204.36.24%252520%2525E4%2525B8%25258B%2525E5%25258D%252588.png)

## 4. Data Preparation

### 4.1 Check Missing Score in Data Set

Although I'm the teacher who record all score, I am not sure if there is missing score in that, so better confirm first:


```
data null;
	set ss_16_17.P5_1_2016_2017_pro;
	array vvv(*) _numeric_;
	do i =1 to dim(vvv);
		if vvv(i)=. then 
		do;
			put name vvv(i)= class year;
			output;
		end;
	end;
run;
```
and here is the log which displayed there are some missing value in the data set:

```
ELYSIA LOUISE PURNOMO t4_ut1=. P5-1 2016-2017
ISABEL KIRANA MARIE t4_ut1=. P5-1 2016-2017
ISABEL KIRANA MARIE t4_oral=. P5-1 2016-2017
ISABEL KIRANA MARIE t4_exam=. P5-1 2016-2017
ISABEL KIRANA MARIE t4_da_class=. P5-1 2016-2017
ISABEL KIRANA MARIE t4_exam_last=. P5-1 2016-2017
JASON JEREMIAH t2_ut1=. P5-1 2016-2017
NOTE: There were 25 observations read from the data set ss_16_17.P5_1_2016_2017_PRO.
NOTE: The data set WORK.NULL has 7 observations and 21 variables.
NOTE: DATA statement used (Total process time):
      real time           0.03 seconds
      cpu time            0.03 seconds
```

explore in SAS data set:  

![](https://cl.ly/8699e397f408/%2525E5%2525B1%25258F%2525E5%2525B9%252595%2525E5%2525BF%2525AB%2525E7%252585%2525A7%2525202019-05-23%252520%2525E4%2525B8%25258B%2525E5%25258D%2525884.43.17.png)

### 4.2 Repair the Missing Value

from above result, we knew some student dont have certain score of dictation or unit test, even term test. Hence I decide the substitute the missing value by some certain score.  

here is the logic:  
> if one of score of student in a certain term is missing, then the mean of all other scores in that certain term will fill the preceding missing value in data set.  

here is code:  

```
data ss_16_17.P5_1_2016_2017_pro;
	set ss_16_17.P5_1_2016_2017_pro;

	select(name);
		when('ELYSIA LOUISE PURNOMO') do;
			t4_ut1=mean(of t4_:);
		end;
		when('JASON JEREMIAH') do;
			t2_ut1=mean(of t2_:);
		end;
		when('ISABEL KIRANA MARIE') do;
			tmp_a=round(mean(of t1:,of t2:,of t3:,of t4:),0.1);
			t4_ut1=tmp_a;
			t4_oral=tmp_a;
			t4_exam=tmp_a;
			t4_da_class=tmp_a;
			t4_exam_last=tmp_a;
		end;
		otherwise;
	end;
	
	drop tmp_a;
run;
```

### 4.3 cross-check the student name in each grade

as preceding explained, the students will be mixed randomly and re-assign to different classes in new grade. so it is necessary to check whether each student name of P5 exist in P6.

here is the code:

```
data all_name_p5;
	set ss_16_17.P5_1_2016_2017_org_done(keep=name class year) ss_16_17.P5_2_2016_2017_org_done(keep=name class year);
run;

data all_name_p6;
	set ss_16_17.P6_1_2017_2018_org_done(keep=name class year) ss_16_17.P6_2_2017_2018_org_done(keep=name class year);
run;

proc sort data=all_name_p5;
by name;

proc sort data=all_name_p6;
by name;

data check_re;
	merge all_name_p5 (in=in_p5) all_name_p6(in=in_p6);
by name;
	if (in_p5 and not in_p6) or (not in_p5 and in_p6);
run;
```

as a result, I found there is a spelling typo of one student:

a student named "CELLINE CHANDRA PRASETYA" in P5 was recorded her score in P6 as name of "CELINE CHANDRA PRASETYA", so I need to modify manually:


```
data ss_16_17.P5_2_2016_2017_pro;
set ss_16_17.P5_2_2016_2017_org_done;
if name='CELLINE CHANDRA PRASETYA' then name='CELINE CHANDRA PRASETYA';
run;
```

### 4.4 Converge several score in each term to one 

There are several dictation score, several unit test score, one daily performance score and one term test score in each term.   
As to unify the statistical source, I decided to converge these above scores to one score for each term by the following formular:



Converge Dictation Test and Unit Test first:  

```
DT = mean(DT_score1,DT_score2,....,DT_scoreN)
UT = mean(UT_score1,UT_score2,....,UT_scoreN)
Daily_score = mean(daily_performance_score, DT)
```
Note:
> I combine the Dictation Test with Daily Performance Score, because dictation test in my school actually cannot indicate the real level of student. some students memorized the dictation word list mechnically and even dont know how to use those words in sentences. So I understand and treat the dictation in our school as a homework. The score of dictation just express the attitude of student to the homework.


then converge totally:

```
Term score = weigh_ut * UT + weigh_ds * Daily_score + weigh_tt * Term_Test
```

here is the code of SAS macro:

```
%MACRO get_average_all_term (input_data=,output_data=,ratio_da_class=,ratio_ut=,ratio_exam=);

data &output_data;
	if &ratio_da_class+&ratio_ut+&ratio_exam ne 1 then do;
		put 'the weighting sum not equal 1 !!';
		stop;
	end;

	set &input_data end=last_row;
	array col_name(*) _numeric_;

	if not ((t1_da_class in col_name) and (t1_ut1 in col_name) and (t1_exam_last in col_name) and (t2_da_class in col_name) and (t2_ut1 in col_name) and (t2_exam_last in col_name) and (t3_da_class in col_name) and (t3_ut1 in col_name) and (t3_exam_last in col_name) and (t4_da_class in col_name) and (t4_ut1 in col_name) and (t4_exam_last in col_name)) then do;
		put 'ERROR!!!!please check all var, there are some missing col';
		stop;
	end;

	score_term1= t1_da_class * &ratio_da_class + t1_ut1 * &ratio_ut + t1_exam_last * &ratio_exam;
	score_term2= t2_da_class * &ratio_da_class + t2_ut1 * &ratio_ut + t2_exam_last * &ratio_exam;
	score_term3= t3_da_class * &ratio_da_class + t3_ut1 * &ratio_ut + t3_exam_last * &ratio_exam;
	score_term4= t4_da_class * &ratio_da_class + t4_ut1 * &ratio_ut + t4_exam_last * &ratio_exam;

	keep name score_term1 score_term2 score_term3 score_term4 class year;
run;

%MEND get_average_all_term;
```

As I used the macro to generate the overall term score for each term, I can assign the different weigh for each kind of score easily, here I assign the weight as:

```
Daily Score	: 10%
Unit Test 	: 40%
Term Test 	: 50%
```

After the weighs are clear, I invoke the marco for each class for P5 and P6:  

```
%get_average_all_term(input_data=ss_16_17.P5_1_2016_2017_bas,output_data=ss_16_17.P5_1_2016_2017_stat_overall,ratio_da_class=0.1,ratio_ut=0.4,ratio_exam=0.5);

%get_average_all_term(input_data=ss_16_17.P5_2_2016_2017_bas,output_data=ss_16_17.P5_2_2016_2017_stat_overall,ratio_da_class=0.1,ratio_ut=0.4,ratio_exam=0.5);

%get_average_all_term(input_data=ss_16_17.P6_1_2017_2018_bas,output_data=ss_16_17.P6_1_2017_2018_stat_overall,ratio_da_class=0.1,ratio_ut=0.4,ratio_exam=0.5);

%get_average_all_term(input_data=ss_16_17.P6_2_2017_2018_bas,output_data=ss_16_17.P6_2_2017_2018_stat_overall,ratio_da_class=0.1,ratio_ut=0.4,ratio_exam=0.5);
```

### 4.5 Merge All students Score Base on Grade P5 & P6

Merge two classes score of P5 to one

```
data all_p5_ss;
	set P5_1_2016_2017_stat_overall 	P5_2_2016_2017_stat_overall;
run;

proc sort data=all_p5_ss;
	by name;
run;
```

Merge two classes score of P6 to one

```
data all_p6_ss;
set P6_1_2017_2018_stat_overall P6_2_2017_2018_stat_overall;
run;

proc sort data=all_p6_ss;
by name;
run;
```

cross-check whether some student score is missing after merge:

```
data test;
merge all_p5_ss(in=in_p5) all_p6_ss(in=in_p6);
by name;
if (in_p5 and not in_p6) or (not in_p5 and in_p6);
run;
```

Then I found there is one student score is missing after she upgrade to P6:

![](https://cl.ly/23f4bce6b2b4/Image%2525202019-05-23%252520at%2525206.19.20%252520%2525E4%2525B8%25258B%2525E5%25258D%252588.png)  

As she is my student, I am aware she transfer out to another school when P5 finished. So I dont plan to analyze her data this time, then ingore it.

### 4.6 Combine 8 term scores in P5 & P6 base on each student name

Merge by students name:

```
data all_ss_p5_2016_2017;
	merge all_p5_ss(in=in_p5 rename=(class=class_16_17 score_term1=p5_term1 score_term2=p5_term2 score_term3=p5_term3 score_term4=p5_term4)) all_p6_ss(in=in_p6 rename=(class=class_17_18 score_term1=p6_term1 score_term2=p6_term2 score_term3=p6_term3 score_term4=p6_term4));
	by name;
	if in_p5 and in_p6;
	drop year;
run;
```

Adjust the sequence of some columns:

```
data all_ss_p5_2016_2017;
	retain  name p5_term1 p5_term2 p5_term3 p5_term4 p6_term1 p6_term2 p6_term3 p6_term4 class_16_17 class_17_18;
	set all_ss_p5_2016_2017;
run;
```

Add two statistical indicators: MEAN and STD

```
data All_ss_p5_2016_2017;
	set All_ss_p5_2016_2017;
	vmean=mean(of p5:,of p6:);
	vstd=std(of p5:,of p6:);
	format vmean 8.1 vstd 8.2;
run;
```


### 4.7 Adjust Statistical Indicator into a Table

```
data ds_stat;
	set all_ss_p5_2016_2017;
	term='p5_term1';
	score=p5_term1;
	output;

	term='p5_term2';
	score=p5_term2;
	output;

	term='p5_term3';
	score=p5_term3;
	output;

	term='p5_term4';
	score=p5_term4;
	output;

	term='p6_term1';
	score=p6_term1;
	output;

	term='p6_term2';
	score=p6_term2;
	output;

	term='p6_term3';
	score=p6_term3;
	output;

	term='p6_term4';
	score=p6_term4;
	output;

	keep term score class_16_17 class_17_18;
run;
```

## 5. Data Modeling

### 5.1 Overall Stat of P5 & P6 with box
```
title 'All Ss P5 & P6 Score Stat(Vbox)';
title2 JUSTIFY=right '(P5 Year2016-2017)';
footnote JUSTIFY=right 'made by Vincent C' ;

proc sgplot data=ds_stat;
	vbox score / category = term;
run;
quit;
```
The Diagram:
![](https://cl.ly/01028305da4f/Image%2525202019-05-22%252520at%25252011.26.02%252520%2525E4%2525B8%25258B%2525E5%25258D%252588.png) 


### 5.2 P5 Score Distribution

```
title 'All Ss P5 Score Distribution';
title2 JUSTIFY=right '(P5 Year2016-2017)';
footnote JUSTIFY=right 'made by Vincent C' ;

PROC SGPANEL DATA=ds_stat;
	PANELBY term / NOVARNAME SPACING = 5;
	ROWAXIS GRID;
	COLAXIS values=(65 to 105 by 5);
	histogram score / BINSTART=70 NBINS=10 BOUNDARY=lower 	DATALABEL=auto;
	density score;
	where term like 'p5%';
run;
```

The Diagram:

![](https://cl.ly/b04f8c277b78/Image%2525202019-05-22%252520at%25252011.48.42%252520%2525E4%2525B8%25258B%2525E5%25258D%252588.png)

### 5.3 P6 Score Distribution

```
title 'All Ss P6 Score Distribution';
title2 JUSTIFY=right '(P5 Year2016-2017)';
footnote JUSTIFY=right 'made by Vincent C' ;

PROC SGPANEL DATA=ds_stat;
	PANELBY term / NOVARNAME SPACING = 5;
	ROWAXIS GRID;
	COLAXIS values=(65 to 105 by 5);
	histogram score / BINSTART=70 NBINS=10 BOUNDARY=lower 	DATALABEL=auto;
	density score;
	where term like 'p6%';
run;
```

The Diagram:

![](https://cl.ly/aad0472ff4a7/Image%2525202019-05-22%252520at%25252011.53.14%252520%2525E4%2525B8%25258B%2525E5%25258D%252588.png)

### 5.4 P5 & P6 Score Distribution  

```
title 'All Ss Score Distribution(During P5/P6)';
title2 JUSTIFY=right '(P5 Year2016-2017)';
footnote JUSTIFY=right 'made by Vincent C' ;

proc sgplot data=ds_stat;
	histogram score / BINSTART=70 NBINS=10 BOUNDARY=lower 	DATALABEL=auto;
	density score;
run;
```

The Diagram:

![](https://cl.ly/40d67ae15df7/Image%2525202019-05-22%252520at%25252011.56.34%252520%2525E4%2525B8%25258B%2525E5%25258D%252588.png) 

### 5.5 Class Comparision in P5 base on Mean  

```
title 'P5 Classes Comparasion(Mean Score)';
title2 JUSTIFY=right '(P5 Year2016-2017)';
footnote JUSTIFY=right 'made by Vincent C' ;

proc SGPLOT data=ds_stat;
	vbar term / group=class_16_17 STAT=mean RESPONSE=score 	GROUPDISPLAY=cluster;
	yaxis values=(70 to 100 by 5);
	where term like 'p5%';
run;
quit;
```

The Diagram:

![](https://cl.ly/4cd1f91b3137/Image%2525202019-05-22%252520at%25252011.14.17%252520%2525E4%2525B8%25258B%2525E5%25258D%252588.png)

### 5.6 Class Comparision in P6 base on Mean  

```
title 'P6 Classes Comparasion(Mean Score)';
title2 JUSTIFY=right '(P5 Year2016-2017)';
footnote JUSTIFY=right 'made by Vincent C' ;

proc SGPLOT data=ds_stat;
	vbar term / group=class_17_18 STAT=mean RESPONSE=score 	GROUPDISPLAY=cluster;
	yaxis values=(70 to 100 by 5);
	where term like 'p6%';
run;
quit;
```

The Diagram:

![](https://cl.ly/0ccea956b721/download/Image%2525202019-05-22%252520at%25252011.18.09%252520%2525E4%2525B8%25258B%2525E5%25258D%252588.png)  

### 5.7 Most unstable score

```
proc sql;
create view v_ss_std_mean_p5_16_17 as
select * from All_ss_p5_2016_2017
order by vstd desc;
run;
quit;
```

```
%let top_num=20;
title "P5/6 Top &top_num Range of Score Variation(Mean & STD)";
title2 JUSTIFY=right '(P5 Year2016-2017)';
footnote JUSTIFY=right 'made by Vincent C' ;
proc SGPLOT data=v_ss_std_mean_p5_16_17(obs=&top_num);
vbarbasic name / RESPONSE=vstd ;
scatter x=name y=vmean / y2axis DATALABEL=vmean DATALABELPOS=TOP;
yaxis values=(1 to 10 by 1) GRID;
y2axis values=(70 to 100 by 5);
/*dont add any where here*/
run;
```

The Diagram:  
![](https://cl.ly/a2fad26d8be6/Image%2525202019-05-22%252520at%25252011.59.23%252520%2525E4%2525B8%25258B%2525E5%25258D%252588.png)


### 5.8 Most stable score

```
proc sql noprint;
select count(*) into :t_num
from All_ss_p5_2016_2017;
quit;
```

```
%let last_num=20;
title "P5/6 Most &last_num Stable of Score Variation(Mean & STD)";
title2 JUSTIFY=right '(P5 Year2016-2017)';
footnote JUSTIFY=right 'made by Vincent C' ;
proc SGPLOT data=v_ss_std_mean_p5_16_17(firstobs=%eval(&t_num-&last_num));
vbarbasic name / RESPONSE=vstd ;
scatter x=name y=vmean / y2axis DATALABEL=vmean DATALABELPOS=TOP;
yaxis values=(0 to 2 by 0.2) GRID;
y2axis values=(70 to 100 by 5);
/*dont add any where here*/
run;
```

The Diagram:  
![](https://cl.ly/6d7140e87aa4/Image%2525202019-05-23%252520at%25252012.00.40%252520%2525E4%2525B8%25258A%2525E5%25258D%252588.png)  

